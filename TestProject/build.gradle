// Copyright 2016 Yahoo Inc.
// Licensed under the terms of the New-BSD license. Please see LICENSE file in the project root for terms.
  
repositories {
   mavenLocal()
}

apply plugin: 'java'
apply plugin: 'maven-publish'

// FIXME: these bits should be a plugin we can reuse.
task buildJni(type:Exec) {
  commandLine 'make'
}

task generateCode(type: JavaExec) {
  classpath sourceSets.main.runtimeClasspath
  classpath '../build/libs/TestUTF8Decoding.jar:build/classes/main/'

  main = 'com.yahoo.wildwest.jnih.ObjectJniH'

  args 'com.yahoo.example.test.SampleInfo'
  args '-javapath'
  args 'src/main/java'
  args '-cfile'
  args 'src/main/native/generateSample'
}

// https://docs.gradle.org/current/userguide/java_plugin.html
buildJni.shouldRunAfter compileJava
compileJava.shouldRunAfter generateCode


build.dependsOn(buildJni)
build.dependsOn(generateCode)


test {
  outputs.upToDateWhen {false}
  
  // enable TestNG support (default is JUnit)
  useTestNG()

  if (System.properties['os.name'] == "Mac OS X") { 
    // set a system property for the test JVM(s)
    systemProperty 'java.library.path','target/native/x86_64-darwin-clang'
  }

  if (System.properties['os.name'] == "Linux") { 
    // set a system property for the test JVM(s)
    systemProperty 'java.library.path','target/native/x86_64-darwin-clang'
  }
}

dependencies {
    compile group: 'com.yahoo.example.codegen', name: 'codegen', version: '1.0.0'
    testCompile group: 'org.testng', name: 'testng', version: '6.8.5'
    runtime group: 'log4j', name: 'log4j', version: '1.2.14'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.7'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.yahoo.testpoject'
            artifactId 'testSample'
            version '1.0.0'

            from components.java
        }
    }
}

